import org.jetbrains.kotlin.gradle.dsl.JvmTarget

plugins {
    id 'com.android.application'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}
repositories {
    mavenCentral()
}
android {
    compileSdk = project.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ksp {
            arg('room.schemaLocation', "$projectDir/schemas")
        }

    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    signingConfigs {
        free {
            storeFile file("../keyStore/release.jks")
            storePassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
            keyAlias "key0"
            keyPassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
        }
        pro {
            storeFile file("../keyStore/release.jks")
            storePassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
            keyAlias "key_pro"
            keyPassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
        }
    }
    flavorDimensions = ["tier"]
    productFlavors {
        free {
            dimension "tier"
            applicationId "vn.unlimit.vpngate"
            signingConfig signingConfigs.free
            versionCode 127
            versionName "2.2.7"
        }

        pro {
            dimension "tier"
            applicationId "vn.unlimit.vpngatepro"
            signingConfig signingConfigs.pro
            versionCode 119
            versionName "2.1.9"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
        }

        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            versionNameSuffix "-dev"
        }
    }
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    namespace 'vn.unlimit.vpngate'
    buildFeatures {
        viewBinding true
        buildConfig = true
    }
}

kotlin {
    compilerOptions {
        jvmTarget = JvmTarget.JVM_17
    }
}

ext.glideVersion = '5.0.5'
dependencies {
    implementation fileTree(include: ['*.jar', '*.so'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.browser:browser:1.9.0'
    implementation 'com.google.code.gson:gson:2.13.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation("com.github.bumptech.glide:glide:$glideVersion@aar") {
        exclude group: "com.android.support"
        transitive = true
    }
    implementation platform('com.google.firebase:firebase-bom:34.8.0')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-analytics'
    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
    implementation 'com.google.guava:guava:33.5.0-android'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.7'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.7'
    implementation 'com.pixplicity.sharp:sharp:1.1.3@aar'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation project(path: ':vpnLib')
    implementation project(path: ':sstpClient')
    implementation project(path: ':SoftEtherClient')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.7.0'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test:rules:1.7.0'
    androidTestImplementation 'androidx.arch.core:core-testing:2.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
    implementation 'com.android.billingclient:billing-ktx:8.3.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.google.android.gms:play-services-ads:24.9.0'
    implementation 'com.google.android.ump:user-messaging-platform:4.0.0'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.squareup.retrofit2:retrofit:3.0.0'
    implementation 'com.squareup.retrofit2:converter-gson:3.0.0'
    implementation 'com.squareup.retrofit2:converter-scalars:3.0.0'
    def room_version = "2.7.1"

    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")
    ksp "androidx.room:room-compiler:$room_version"

}
