plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.devtools.ksp'
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}
repositories {
    mavenCentral()
}
android {
    compileSdk project.ext.compileSdkVersion
    defaultConfig {
        minSdkVersion project.ext.minSdkVersion
        targetSdkVersion project.ext.targetSdkVersion
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        ksp {
            arg('room.schemaLocation', "$projectDir/schemas")
        }

    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging = true
        }
    }
    signingConfigs {
        free {
            storeFile file("../keyStore/release.jks")
            storePassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
            keyAlias "key0"
            keyPassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
        }
        pro {
            storeFile file("../keyStore/release.jks")
            storePassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
            keyAlias "key_pro"
            keyPassword String.valueOf(System.getenv("SIGN_KEY_PASSWORD"))
        }
    }
    flavorDimensions = ["tier"]
    productFlavors {
        free {
            dimension "tier"
            applicationId "vn.unlimit.vpngate"
            signingConfig signingConfigs.free
            versionCode 117
            versionName "2.1.7"
        }

        pro {
            dimension "tier"
            applicationId "vn.unlimit.vpngatepro"
            signingConfig signingConfigs.pro
            versionCode 110
            versionName "2.1.0"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            manifestPlaceholders = [crashlyticsCollectionEnabled: "true"]
        }

        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled: "false"]
            versionNameSuffix "-dev"
        }
    }
    useLibrary 'android.test.runner'

    useLibrary 'android.test.base'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    namespace 'vn.unlimit.vpngate'
    buildFeatures {
        viewBinding true
        buildConfig = true
    }
}
ext.glideVersion = '4.16.0'
dependencies {
    implementation fileTree(include: ['*.jar', '*.so'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.browser:browser:1.8.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation("com.github.bumptech.glide:glide:$glideVersion@aar") {
        exclude group: "com.android.support"
        transitive = true
    }
    implementation platform('com.google.firebase:firebase-bom:33.15.0')
    implementation 'com.google.firebase:firebase-messaging'
    implementation 'com.google.firebase:firebase-config'
    implementation 'com.google.firebase:firebase-analytics'
    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics'
    implementation 'com.google.android.play:app-update-ktx:2.1.0'
    implementation 'com.google.guava:guava:33.3.0-android'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.9.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.9.0'
    implementation 'com.pixplicity.sharp:sharp:1.1.3@aar'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation project(path: ':vpnLib')
    implementation project(path: ':sstpClient')
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:runner:1.6.2'
    def billing_version = '7.1.1'
    implementation "com.android.billingclient:billing-ktx:$billing_version"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    ksp "com.github.bumptech.glide:ksp:$glideVersion"
    implementation 'com.google.android.gms:play-services-ads:24.3.0'
    implementation 'com.google.android.ump:user-messaging-platform:3.2.0'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'com.squareup.retrofit2:retrofit:2.11.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.11.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.11.0'
    def room_version = "2.7.1"

    implementation("androidx.room:room-runtime:$room_version")
    annotationProcessor("androidx.room:room-compiler:$room_version")
    ksp "androidx.room:room-compiler:$room_version"

}
